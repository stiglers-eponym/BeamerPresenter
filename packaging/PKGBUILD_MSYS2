# Maintainer: Valentin Bruch <software@vbruch.eu>
# Recipe file for building BeamerPresenter in Windows using MSYS2.

: ${_qt_version_major:=6} # 5 or 6
: ${_use_mupdf:=ON} # ON or OFF
: ${_use_poppler:=OFF} # ON or OFF

_renderer=''
if [ "${_use_mupdf}" == 'ON' ]
then
    _renderer="${_renderer}-mupdf"
fi
if [ "${_use_poppler}" == 'ON' ]
then
    _renderer="${_renderer}-poppler"
fi
_basename='beamerpresenter'
_pkgname="${_basename}${_renderer}-qt${_qt_version_major}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_pkgname}"
pkgver=0.2.6
pkgrel=1
pkgdesc="Modular multi-screen pdf presenter (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/stiglers-eponym/BeamerPresenter"
license=('spdx:GPL-3.0-or-later')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake" "${MINGW_PACKAGE_PREFIX}-ninja" "${MINGW_PACKAGE_PREFIX}-qt${_qt_version_major}-tools")
depends=("${MINGW_PACKAGE_PREFIX}-qt${_qt_version_major}-multimedia" "${MINGW_PACKAGE_PREFIX}-qt${_qt_version_major}-svg")
provides=("beamerpresenter=${pkgver}")
conflicts=('beamerpresenter')
backup=("etc/xdg/beamerpresenter/beamerpresenter.conf" "etc/xdg/beamerpresenter/gui.json")
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('SKIP')

if [ "${_use_mupdf}" == 'ON' ]
then
    makedepends+=("${MINGW_PACKAGE_PREFIX}-mupdf-libmupdf")
    depends+=("${MINGW_PACKAGE_PREFIX}-jbig2dec" "${MINGW_PACKAGE_PREFIX}-openjpeg2" "${MINGW_PACKAGE_PREFIX}-gumbo-parser" )
    license=('spdx:GPL-3.0-or-later AND AGPL-3.0')
fi

if [ "${_use_poppler}" == 'ON' ]
then
    depends+=("${MINGW_PACKAGE_PREFIX}-poppler-qt${_qt_version_major}")
fi

build() {
    mkdir -p "${srcdir}/build-${MSYSTEM}"
    MSYS2_ARG_CONV_EXCL='-DCMAKE_INSTALL_PREFIX=;-DCMAKE_INSTALL_SYSCONFDIR=;' \
      "${MINGW_PREFIX}/bin/cmake.exe" \
        -B "${srcdir}/build-${MSYSTEM}" \
        -S "${srcdir}/BeamerPresenter" \
        -G Ninja \
        -DCMAKE_BUILD_TYPE='Release' \
        -DGIT_VERSION=OFF \
        -DUSE_POPPLER="${_use_poppler}" \
        -DUSE_MUPDF="${_use_mupdf}" \
        -DUSE_QTPDF=OFF \
        -DUSE_EXTERNAL_RENDERER=OFF \
        -DUSE_TRANSLATIONS=ON \
        -DINSTALL_LICENSE=ON \
        -DLINK_MUPDF_THIRD=ON \
        -DLINK_MUJS=OFF \
        -DLINK_GUMBO=ON \
        -DQT_VERSION_MAJOR="${_qt_version_major}" \
        -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
        -DCMAKE_INSTALL_SYSCONFDIR="/etc"
    "${MINGW_PREFIX}/bin/cmake.exe" --build "${srcdir}/build-${MSYSTEM}"
}

package() {
    DESTDIR="${pkgdir}" "${MINGW_PREFIX}/bin/cmake.exe" --install "${srcdir}/build-${MSYSTEM}"
}
